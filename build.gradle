plugins {
    id 'java'
}

// 모든 하위 프로젝트(Core, TestAddon)에 공통 적용
subprojects {
    apply plugin: 'java'

    group = 'com.server'
    version = '1.0.0-SNAPSHOT'

    repositories {
        mavenCentral()
        maven { url 'https://repo.papermc.io/repository/maven-public/' }
        maven { url 'https://oss.sonatype.org/content/groups/public/' }
    }

    dependencies {
        // Paper API 1.21.4
        compileOnly 'io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT'
    }

    java {
        toolchain.languageVersion = JavaLanguageVersion.of(21)
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    processResources {
        def props = [version: version]
        inputs.properties props
        filteringCharset 'UTF-8'
        filesMatching('plugin.yml') {
            expand props
        }
    }
}